apply plugin: 'java'
apply plugin: 'idea'

def langLevel = 1.7
sourceCompatibility = langLevel

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.11'
	testCompile  'org.mockito:mockito-all:1.9.5'
}

test {
	exclude '**/*IntegrationTest.*'
}

task integrationTest(dependsOn: jar, type: Test) {
	include '**/*IntegrationTest.*'
}

task allTests(dependsOn: inOrder(test, integrationTest))

tasks.withType(Test) {
	maxParallelForks = 4
	testLogging {
		events "passed", "skipped", "failed"
	}
}

def inOrder(Task... dependencies) {
	dependencies.toList().collate(2, 1, false).each { first, second ->
		second.mustRunAfter(first)
	}
}

idea {
	project {
		jdkName = langLevel
		languageLevel = langLevel
	}
}